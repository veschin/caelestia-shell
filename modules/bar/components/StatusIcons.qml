pragma ComponentBehavior: Bound

import qs.components
import qs.components.controls
import qs.services
import qs.utils
import Caelestia.Config
import Quickshell
import Quickshell.Bluetooth
import Quickshell.Io
import Quickshell.Services.UPower
import QtQuick
import QtQuick.Layouts

StyledRect {
    id: root

    property color colour: Colours.palette.m3secondary
    readonly property alias items: iconColumn

    color: Colours.tPalette.m3surfaceContainer
    radius: Tokens.rounding.full

    clip: true
    implicitWidth: Tokens.sizes.bar.innerWidth
    implicitHeight: iconColumn.implicitHeight + Tokens.padding.normal * 2 - (Config.bar.status.showLockStatus && !Hypr.capsLock && !Hypr.numLock ? iconColumn.spacing : 0)

    ColumnLayout {
        id: iconColumn

        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        anchors.bottomMargin: Tokens.padding.normal

        spacing: Tokens.spacing.smaller / 2

        // Lock keys status
        WrappedLoader {
            name: "lockstatus"
            active: Config.bar.status.showLockStatus

            sourceComponent: ColumnLayout {
                spacing: 0

                Item {
                    implicitWidth: capslockIcon.implicitWidth
                    implicitHeight: Hypr.capsLock ? capslockIcon.implicitHeight : 0

                    MaterialIcon {
                        id: capslockIcon

                        anchors.centerIn: parent

                        scale: Hypr.capsLock ? 1 : 0.5
                        opacity: Hypr.capsLock ? 1 : 0

                        text: "keyboard_capslock_badge"
                        color: root.colour

                        Behavior on opacity {
                            Anim {}
                        }

                        Behavior on scale {
                            Anim {}
                        }
                    }

                    Behavior on implicitHeight {
                        Anim {}
                    }
                }

                Item {
                    Layout.topMargin: Hypr.capsLock && Hypr.numLock ? iconColumn.spacing : 0

                    implicitWidth: numlockIcon.implicitWidth
                    implicitHeight: Hypr.numLock ? numlockIcon.implicitHeight : 0

                    MaterialIcon {
                        id: numlockIcon

                        anchors.centerIn: parent

                        scale: Hypr.numLock ? 1 : 0.5
                        opacity: Hypr.numLock ? 1 : 0

                        text: "looks_one"
                        color: root.colour

                        Behavior on opacity {
                            Anim {}
                        }

                        Behavior on scale {
                            Anim {}
                        }
                    }

                    Behavior on implicitHeight {
                        Anim {}
                    }
                }
            }
        }

        // Audio icon
        WrappedLoader {
            name: "audio"
            active: Config.bar.status.showAudio

            sourceComponent: MaterialIcon {
                animate: true
                text: Icons.getVolumeIcon(Audio.volume, Audio.muted)
                color: root.colour
            }
        }

        // Microphone icon
        WrappedLoader {
            name: "audio"
            active: Config.bar.status.showMicrophone

            sourceComponent: MaterialIcon {
                animate: true
                text: Icons.getMicVolumeIcon(Audio.sourceVolume, Audio.sourceMuted)
                color: root.colour
            }
        }

        // Keyboard layout icon
        WrappedLoader {
            name: "kblayout"
            active: Config.bar.status.showKbLayout

            sourceComponent: StyledText {
                animate: true
                text: Hypr.kbLayout
                color: root.colour
                font.family: Tokens.font.family.mono
            }
        }

        // Network icon
        WrappedLoader {
            name: "network"
            active: Config.bar.status.showNetwork && (!Nmcli.activeEthernet || Config.bar.status.showWifi)

            sourceComponent: MaterialIcon {
                animate: true
                text: Nmcli.active ? Icons.getNetworkIcon(Nmcli.active.strength ?? 0) : "wifi_off"
                color: root.colour
            }
        }

        // Ethernet icon
        WrappedLoader {
            name: "ethernet"
            active: Config.bar.status.showNetwork && Nmcli.activeEthernet

            sourceComponent: MaterialIcon {
                animate: true
                text: "cable"
                color: root.colour
            }
        }

        // Bluetooth section
        WrappedLoader {
            Layout.preferredHeight: implicitHeight

            name: "bluetooth"
            active: Config.bar.status.showBluetooth

            sourceComponent: ColumnLayout {
                spacing: Tokens.spacing.smaller / 2

                // Bluetooth icon
                MaterialIcon {
                    animate: true
                    text: {
                        if (!Bluetooth.defaultAdapter?.enabled)
                            return "bluetooth_disabled";
                        if (Bluetooth.devices.values.some(d => d.connected))
                            return "bluetooth_connected";
                        return "bluetooth";
                    }
                    color: root.colour
                }

                // Connected bluetooth devices
                Repeater {
                    model: ScriptModel {
                        values: Bluetooth.devices.values.filter(d => d.state !== BluetoothDeviceState.Disconnected)
                    }

                    MaterialIcon {
                        id: device

                        required property BluetoothDevice modelData

                        animate: true
                        text: Icons.getBluetoothIcon(modelData?.icon)
                        color: root.colour
                        fill: 1

                        SequentialAnimation on opacity {
                            running: device.modelData?.state !== BluetoothDeviceState.Connected
                            alwaysRunToEnd: true
                            loops: Animation.Infinite

                            Anim {
                                from: 1
                                to: 0
                                duration: Tokens.anim.durations.large
                                easing: Tokens.anim.standardAccel
                            }
                            Anim {
                                from: 0
                                to: 1
                                duration: Tokens.anim.durations.large
                                easing: Tokens.anim.standardDecel
                            }
                        }
                    }
                }
            }

            Behavior on Layout.preferredHeight {
                Anim {}
            }
        }

        // Battery icon
        WrappedLoader {
            name: "battery"
            active: Config.bar.status.showBattery

            sourceComponent: MaterialIcon {
                animate: true
                text: {
                    if (!UPower.displayDevice.isLaptopBattery) {
                        if (PowerProfiles.profile === PowerProfile.PowerSaver)
                            return "energy_savings_leaf";
                        if (PowerProfiles.profile === PowerProfile.Performance)
                            return "rocket_launch";
                        return "balance";
                    }

                    const perc = UPower.displayDevice.percentage;
                    const charging = [UPowerDeviceState.Charging, UPowerDeviceState.FullyCharged, UPowerDeviceState.PendingCharge].includes(UPower.displayDevice.state);
                    if (perc === 1)
                        return charging ? "battery_charging_full" : "battery_full";
                    let level = Math.floor(perc * 7);
                    if (charging && (level === 4 || level === 1))
                        level--;
                    return charging ? `battery_charging_${(level + 3) * 10}` : `battery_${level}_bar`;
                }
                color: !UPower.onBattery || UPower.displayDevice.percentage > 0.2 ? root.colour : Colours.palette.m3error
                fill: 1
            }
        }

        // VPN status
        WrappedLoader {
            name: "vpn"
            active: Config.bar.status.showVpn !== false

            sourceComponent: Item {
                id: vpnItem

                property bool vpnActive: false

                implicitWidth: vpnIcon.implicitWidth
                implicitHeight: vpnIcon.implicitHeight

                MaterialIcon {
                    id: vpnIcon

                    anchors.centerIn: parent
                    animate: true
                    text: vpnItem.vpnActive ? "vpn_key" : "vpn_key_off"
                    color: vpnItem.vpnActive ? Colours.palette.m3primary : root.colour
                }

                Timer {
                    running: true
                    repeat: true
                    interval: 5000
                    triggeredOnStart: true
                    onTriggered: vpnProc.running = true
                }

                Process {
                    id: vpnProc

                    command: ["bash", Quickshell.env("HOME") + "/.local/bin/vpn-status.sh"]
                    stdout: StdioCollector {
                        onStreamFinished: {
                            try {
                                const data = JSON.parse(text);
                                vpnItem.vpnActive = data.anyActive;
                            } catch (e) {
                                vpnItem.vpnActive = false;
                            }
                        }
                    }
                }
            }
        }

        // Claude API limits
        WrappedLoader {
            name: "claude"
            active: Config.bar.status.showClaude !== false

            sourceComponent: Item {
                id: claudeItem

                property real fiveHourUtil: -1
                property string resetsAt: ""
                property real lastFetchTime: 0
                property bool isLoading: false
                property int tick: 0

                readonly property color colour: {
                    if (fiveHourUtil >= 80) return Colours.palette.m3error;
                    if (fiveHourUtil >= 50) return Colours.palette.m3tertiary;
                    return root.colour;
                }

                property string resetStr: {
                    void(tick);
                    return formatReset(resetsAt);
                }

                property string tooltipStr: {
                    if (fiveHourUtil < 0)
                        return isLoading ? "загрузка..." : "нет данных";
                    if (fiveHourUtil === 0)
                        return "лимиты свободны";
                    return Math.round(fiveHourUtil) + "%" + (resetStr ? " | сброс через " + resetStr : "");
                }

                function shouldFetch(): bool {
                    return Date.now() - lastFetchTime > 120000;
                }

                function formatReset(iso: string): string {
                    if (!iso) return "";
                    const diff = new Date(iso) - new Date();
                    if (diff <= 0) return "скоро";
                    const h = Math.floor(diff / 3600000);
                    const m = Math.floor((diff % 3600000) / 60000);
                    if (h > 0) return h + "ч " + m + "м";
                    return m + "м";
                }

                function parseData(raw: string): void {
                    try {
                        const data = JSON.parse(raw);
                        if (!data.error && data.five_hour) {
                            fiveHourUtil = data.five_hour.utilization;
                            resetsAt = data.five_hour.resets_at ?? "";
                        }
                    } catch (e) {}
                }

                implicitWidth: claudeIcon.implicitWidth
                implicitHeight: claudeIcon.implicitHeight

                MaterialIcon {
                    id: claudeIcon

                    anchors.centerIn: parent
                    text: "smart_toy"
                    color: claudeItem.colour

                    SequentialAnimation on opacity {
                        running: claudeItem.isLoading
                        loops: Animation.Infinite
                        alwaysRunToEnd: true

                        Anim {
                            from: 1
                            to: 0.3
                            duration: Tokens.anim.durations.large
                            easing: Tokens.anim.standardAccel
                        }
                        Anim {
                            from: 0.3
                            to: 1
                            duration: Tokens.anim.durations.large
                            easing: Tokens.anim.standardDecel
                        }
                    }
                }

                MouseArea {
                    id: mouseArea
                    anchors.fill: parent
                    hoverEnabled: true
                    acceptedButtons: Qt.NoButton
                    onContainsMouseChanged: {
                        if (containsMouse && claudeItem.shouldFetch()) {
                            claudeItem.isLoading = true;
                            claudeProc.running = true;
                        }
                    }
                }

                Tooltip {
                    target: claudeItem
                    text: claudeItem.tooltipStr
                }

                FileView {
                    path: Quickshell.env("HOME") + "/.cache/claude-limits.json"
                    onLoaded: claudeItem.parseData(text())
                    watchChanges: true
                    onFileChanged: reload()
                }

                Timer {
                    running: true
                    repeat: true
                    interval: 120000
                    triggeredOnStart: true
                    onTriggered: claudeProc.running = true
                }

                Timer {
                    running: claudeItem.resetsAt !== ""
                    repeat: true
                    interval: 60000
                    onTriggered: claudeItem.tick++
                }

                Process {
                    id: claudeProc
                    command: ["bash", Quickshell.env("HOME") + "/.local/bin/claude-limits.sh"]
                    stdout: StdioCollector {
                        onStreamFinished: {
                            claudeItem.isLoading = false;
                            claudeItem.lastFetchTime = Date.now();
                            claudeItem.parseData(text);
                        }
                    }
                }
            }
        }

        WrappedLoader {
            name: "web3"
            active: true
            sourceComponent: Web3Progress {
                colour: root.colour
            }
        }
    }

    component WrappedLoader: Loader {
        required property string name

        Layout.alignment: Qt.AlignHCenter
        visible: active
    }
}
